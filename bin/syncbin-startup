#!/bin/zsh

DESCRIPTION=$'Run startup scripts for Fenhl\'s syncbin.'

USAGE=$'Usage:
. syncbin-startup [options]
  syncbin-startup -h | --help'

OPTIONS=$'Options:
  -h, --help          Print this message and exit.
  --ignore-lock       Release and ignore the lock that prevents this script from running multiple times at once.
  --no-internet-test  Do not run `syncbin-hasinet\' to test for internet connectivity, but run all other startup scripts regardless.'

function has-cronjob {
    if crontab -l |& grep "$1" &> /dev/null; then
        return 0
    elif sudo -n crontab -l |& grep "$1" &> /dev/null; then
        return 0
    else
        return 1
    fi
}

function is-omz-plugin {
    [[ -f "$1"/plugins/"$2"/"$2".plugin.zsh ]] || [[ -f "$1"/plugins/"$2"/_"$2" ]]
}

function print-progress {
    if where clear-eol &> /dev/null && where py-where &> /dev/null && py-where blessings &> /dev/null; then
        printf '['"$1"'] '
        clear-eol
        printf "$2"'\r'
    else
        printf '['"$1"'] '"$2"'\r'
    fi
}

function print-warning {
    if where clear-eol &> /dev/null && where py-where &> /dev/null && py-where blessings &> /dev/null; then
        printf '[ !! ] '
        clear-eol
        printf 'warning: '"$1"'\n'
    else
        printf '[ !! ] warning: '"$1"'\n'
    fi
    print-progress "$2" "$3"
}

function lockdir {
    echo /tmp/syncbin-startup-"$1".lock
}

function lock {
    if [[ "$IGNORELOCK" == "0" ]]; then
        print-warning 'releasing startup lock' "$2" 'acquiring startup lock: '"$1"
        rm -rf "$lockdir"
    else
        print-progress "$2" 'acquiring startup lock: '"$1"
        until mkdir "$(lockdir $1)" &> /dev/null; do
            sleep 1
            if [[ "$2" == '=...' ]]; then
                printf '[.=..]\r'
                sleep 1
                printf '[..=.]\r'
                sleep 1
                printf '[...=]\r'
                sleep 1
                printf '[..=.]\r'
                sleep 1
                printf '[.=..]\r'
                sleep 1
                printf '[=...]\r'
            fi
        done
        trap 'rm -rf '"$(lockdir $1)" HUP TERM INT # remove lock when script finishes
    fi
}

function try-lock {
    if [[ "$IGNORELOCK" == "0" ]]; then
        print-warning 'releasing startup lock' "$2" 'acquiring startup lock: '"$1"
        rm -rf "$lockdir"
    else
        if mkdir "$(lockdir $1)" &> /dev/null; then
            trap 'rm -rf '"$(lockdir $1)" HUP TERM INT # remove lock when script finishes
        else
            return 1
        fi
    fi
}

function unlock {
    rmdir "$(lockdir $1)" # remove lock
    trap ':' HUP TERM INT # neutralize trap
}

IGNOREINET=1
IGNORELOCK=1

while [[ $# -gt "0" ]]; do
    if [[ "$1" == "-h" ]] || [[ "$1" == "--help" ]]; then
        echo "$DESCRIPTION"
        echo
        echo "$USAGE"
        echo
        echo "$OPTIONS"
        exit 0
    fi
    if [[ "$1" == "--no-internet-test" ]]; then
        IGNOREINET=0
    elif [[ "$1" == "--ignore-lock" ]]; then
        IGNORELOCK=0
    else
        echo $USAGE >&2
        exit 1
    fi
    shift
done

printf '[=...] checking for required Python packages      \r'

SYNCBIN_MISSING_PYTHON_PACKAGE=1

function test-missing-python-package {
    local module_name="$1"
    local package_name="$1"
    if [[ $# -ge 2 ]]; then
        package_name="$2"
    fi
    if ! py-where "$module_name" &> /dev/null; then
        if [[ "$SYNCBIN_MISSING_PYTHON_PACKAGE" == "1" ]]; then
            if where clear-eol &> /dev/null; then
                clear-eol
            fi
            printf '[ !! ] missing Python packages: '
            SYNCBIN_MISSING_PYTHON_PACKAGE=0
        else
            printf ', '
        fi
        printf "$package_name"
    fi
}

if ! where py-where &> /dev/null; then
    echo '[ !! ] failed to check for required Python packages: py-where not found'
elif ! py-where blessings &> /dev/null; then
    echo '[ !! ] missing Python package “blessings”, install with `[sudo ]pip3 install blessings`'
else
    test-missing-python-package basedir 'fenhl/python-xdg-basedir'
    test-missing-python-package fancyio 'fenhl/fancyio'
    test-missing-python-package docopt
    test-missing-python-package lazyjson 'fenhl/lazyjson'
    test-missing-python-package mpd 'python-mpd2'
    test-missing-python-package pytz
    test-missing-python-package requests
    test-missing-python-package tzlocal
    if [[ "$SYNCBIN_MISSING_PYTHON_PACKAGE" == "0" ]]; then
        printf '\n'
    fi
fi

lock ssh '==..'
print-progress '==..' 'running startup script: ssh'
if ! diff ~/.ssh/config "$GITDIR/github.com/fenhl/syncbin/master/config/ssh" > /dev/null; then
    cp "$GITDIR/github.com/fenhl/syncbin/master/config/ssh" ~/.ssh/config || print-warning 'outdated SSH config' '==..' 'running startup script: ssh'
    chmod 600 ~/.ssh/config || print-warning 'wrong permissions on SSH config' '==..' 'running startup script: ssh'
fi
unlock ssh

lock syncbin-omz '==..'
print-progress '==..' 'running startup script: syncbin hasinet'
if [[ "$IGNOREINET" == "1" ]] && where syncbin-hasinet &> /dev/null; then
    syncbin-hasinet
    SYNCBINHASINET=$?
else
    SYNCBINHASINET=0
fi
unlock syncbin-omz

if [[ $SYNCBINHASINET -eq 0 ]]; then
    if where apt-get-wrapper &> /dev/null; then
        lock debian '===.'
        print-progress '===.' 'running startup script: apt'
        apt-get-wrapper
        unlock debian
    fi
    # softwareupdate consistently taking over a minute, disabling for now
    #if where softwareupdate-wrapper &> /dev/null; then
    #    try-lock softwareupdate '===.' && { # don't wait for this lock, one check at a time is enough
    #        print-progress '===.' 'running startup script: softwareupdate'
    #        softwareupdate-wrapper
    #        unlock softwareupdate
    #    }
    #fi
    lock softwareupdate '===.'
    print-progress '===.' 'running startup script: xcode-select'
    if where xcode-select &> /dev/null; then
        if xcode-select --install &> /dev/null; then
            print-warning 'installing command-line tools, open Xcode.app to complete' '===.' 'running startup script: xcode-select'
        fi
    fi
    unlock softwareupdate
    lock ruby '===.'
    print-progress '===.' 'running startup script: brew'
    if has-cronjob 'brew-wrapper'; then
        : # brew upgrade has a cronjob, don't upgrade now
    elif where brew &> /dev/null; then
        brew-wrapper --startup
    fi
    if where brew &> /dev/null; then
        if brew command command-not-found-init &> /dev/null; then
            eval "$(brew command-not-found-init)"
        else
            print-warning 'brew is missing command-not-found support, install with `brew tap homebrew/command-not-found`'
        fi
    elif [[ "$(uname -s)" == "Darwin" ]]; then
        print-warning 'brew not installed, see http://brew.sh/ for instructions'
    fi
    print-progress '===.' 'running startup script: gem'
    if [[ "$(uname -s)" != "Darwin" ]] && where gem-wrapper &> /dev/null; then
        gem-wrapper
    fi
    unlock ruby
    print-progress '===.' 'running startup script: rust'
    # rust has its own lock
    if where rustup &> /dev/null; then
        if ! rustup override list | grep 'no overrides' &> /dev/null; then
            print-warning 'Rust toolchain overridden for '"$(pwd)" '===.' 'running startup script: rustup'
        elif [[ "$(rust default)" != "stable" ]]; then
            print-warning 'Rust not defaulting to stable' '===.' 'running startup script: rustup'
        fi
        rust --quiet --no-project --skip-if-locked
    elif where rustc &> /dev/null; then
        print-warning 'rustup not installed' '===.' 'running startup script: rustup'
    fi
    print-progress '===.' 'running startup script: gitdir'
    if [[ -d /opt/hub ]]; then
        print-warning '/opt/hub exists' '===.' 'running startup script: gitdir'
    fi
    if [[ -d ~/hub ]]; then
        print-warning '~/hub exists' '===.' 'running startup script: gitdir'
    fi
    if has-cronjob 'gitdir update --quiet' || has-cronjob "${GITDIR}/github.com/fenhl/gitdir/master/gitdir/__main__.py update --quiet"; then
        : # gitdir update has a cronjob, don't update now
    elif where gitdir &> /dev/null; then
        if try-lock gitdir '===.'; then
            gitdir update &> /dev/null
        else
            lock gitdir '===.'
        fi
        unlock gitdir
    else
        print-warning 'gitdir not installed' '===.' 'running startup script: gitdir'
    fi
    lock syncbin-omz '===.'
    print-progress '===.' 'running startup script: syncbin update'
    if [[ -d /opt/git/fenhl.net/syncbin-private/master ]] || [[ -d ~/git/fenhl.net/syncbin-private/master ]]; then
        syncbin update
    else
        syncbin update public
    fi
    print-progress '===.' 'running startup script: oh-my-zsh'
    oh-my-zsh update
    unlock syncbin-omz
else
    print-warning 'no internet connection, skipping some startup scripts' '==..' 'running startup script: syncbin hasinet'
fi

lock syncbin-omz '===='
print-progress '====' 'running startup script: oh-my-zsh'
if [[ -d ${HOME}/git/github.com/robbyrussell/oh-my-zsh/master ]] || [[ -d /opt/git/github.com/robbyrussell/oh-my-zsh/master ]]; then
    for plugin ($OMZ_PLUGINS); do
        if is-omz-plugin ${HOME}/git/github.com/robbyrussell/oh-my-zsh/master $plugin; then
            fpath=(${HOME}/git/github.com/robbyrussell/oh-my-zsh/master/plugins/$plugin $fpath)
        elif is-omz-plugin /opt/git/github.com/robbyrussell/oh-my-zsh/master $plugin; then
            fpath=(/opt/git/github.com/robbyrussell/oh-my-zsh/master/plugins/$plugin $fpath)
        else
            print-warning 'unknown oh-my-zsh plugin: '"$plugin" '====' 'running startup script: oh-my-zsh'
        fi
    done
else
    print-warning 'oh-my-zsh not installed' '====' 'running startup script: oh-my-zsh'
fi
print-progress '====' 'running startup script: sip'
if where csrutil &> /dev/null; then
    if csrutil status | grep enabled > /dev/null; then
        print-warning 'System Integrity Protection is enabled' '===.' 'running startup script: sip'
    fi
fi
print-progress '====' 'running startup script: compinit'
autoload -U compinit
compinit -D -u
print-progress '====' 'running startup script: aliases'
. aliases-zsh
. aliases-zsh-suffix
unlock syncbin-omz

print-progress '====' 'running startup script: syncbin-private'
if [[ -d /opt/git/fenhl.net/syncbin-private/master ]]; then
    if [[ -x /opt/git/fenhl.net/syncbin-private/master/syncbin-startup ]]; then
        . /opt/git/fenhl.net/syncbin-private/master/syncbin-startup
    fi
elif [[ -d ~/git/fenhl.net/syncbin-private/master ]]; then
    if [[ -x ~/git/fenhl.net/syncbin-private/master/syncbin-startup ]]; then
        . ~/git/fenhl.net/syncbin-private/master/syncbin-startup
    fi
else
    print-warning 'syncbin-private not installed, install with `syncbin bootstrap syncbin-private`' '====' 'running startup script: syncbin-private'
fi

print-progress '====' 'running startup script: zshrc'
if where zshrc &> /dev/null; then
    . zshrc
fi
